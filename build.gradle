/*
 * CSC Nsk Java 2017
 * Created by frikadelki on 2017/09/xx
 */

allprojects {
    repositories {
        mavenCentral()
    }

    group 'org.csc.java2017'
}

ext.libsVersions = [
    lombok  : '1.16.12',
    junit   : '4.12',
]
ext.libs = [
    lombok  : "org.projectlombok:lombok:${libsVersions.lombok}",
    junit   : "junit:junit:${libsVersions.junit}"
]

subprojects {
    apply plugin: 'java'
    sourceCompatibility = JavaVersion.VERSION_1_8
    sourceSets {
        test {
            java {
                srcDirs 'src/teacher-test/java'
            }
        }
    }
    test {
        testLogging.showStandardStreams = true
    }

    apply plugin: 'checkstyle'
    tasks.withType(Checkstyle) {
        def configDir = project.rootProject.file('config/checkstyle')
        configProperties = ['config-dir' : configDir]
        configFile = new File(configDir, 'checkstyle.xml')
        showViolations = true
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    dependencies {
        testCompile libs.junit
        testCompile libs.lombok
    }
}
